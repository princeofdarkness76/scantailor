# SCANTAILOR_EXE and CRASHREPORTER_EXE are passed as parameters.
!define VERSION "@VERSION@"
!define VERSION_QUAD "@VERSION_QUAD@"
!define LICENSE_FILE "@LICENSE_FILE@"
!define MINGWM10_DLL "@MINGWM10_DLL@"
!define CRT_REDIST_PATH "@CRT_REDIST_PATH@"
!define CRT_REDIST_DIR "@CRT_REDIST_DIR@"
!define INSTALLER_FILENAME "@INSTALLER_FILENAME@"
!define UNINST_REGKEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\Scan Tailor"

# The window identifier of the main Scan Tailor window.
!define WNDCLASS "ScanTailor_Window"

!include "@REGISTER_EXTENSION_NSH@"

!include "MUI2.nsh"
BrandingText " " # To remove the mention of NullSoft.
#LoadLanguageFile "${NSISDIR}\Contrib\Language files\Russian.nlf"

Name "Scan Tailor version ${VERSION}"

OutFile "${INSTALLER_FILENAME}"

installDir "$PROGRAMFILES\Scan Tailor"
InstallDirRegKey HKLM "${UNINST_REGKEY}" "InstallLocation"

RequestExecutionLevel admin

XPStyle on

AllowSkipFiles off

SetCompressor /SOLID /FINAL lzma

!if "${VERSION_QUAD}" != ""
  VIAddVersionKey "ProductName" "Scan Tailor"
  VIAddVersionKey "ProductVersion" "${VERSION}"
  VIAddVersionKey "Comments" "Interactive post-processing tool for scanned pages."
  VIAddVersionKey "CompanyName" "Joseph Artsimovich"
  VIAddVersionKey "LegalTrademarks" ""
  VIAddVersionKey "LegalCopyright" "ï¿½ Joseph Artsimovich et al."
  VIAddVersionKey "FileDescription" "Post-processing tool for scanned pages."
  VIAddVersionKey "FileVersion" "${VERSION}"
  VIProductVersion "${VERSION_QUAD}"
!endif

!define MUI_ABORTWARNING

# Installer Pages
!insertmacro MUI_PAGE_LICENSE "${LICENSE_FILE}"
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES

!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES

!insertmacro MUI_LANGUAGE "English"

Section
  SetOutPath $INSTDIR
  File "${SCANTAILOR_EXE}"
  !if "${MINGWM10_DLL}" != ""
    File "${MINGWM10_DLL}"
  !endif
  !if "${CRASHREPORTER_EXE}" != ""
    File "${CRASHREPORTER_EXE}"
  !endif
  !if "${CRT_REDIST_PATH}" != ""
	File /r "${CRT_REDIST_PATH}"
  !endif
  SetOutPath "$INSTDIR\translations"
  File *.qm
  
  WriteUninstaller $INSTDIR\Uninstaller.exe
  
  # Write uninstall registry records.
  WriteRegStr HKLM "${UNINST_REGKEY}" "DisplayName" "Scan Tailor"
  WriteRegStr HKLM "${UNINST_REGKEY}" "UninstallString" "$INSTDIR\Uninstaller.exe"
  
  # Create menu shortcuts.
  SetShellVarContext all
  CreateDirectory "$SMPROGRAMS\Scan Tailor"
  CreateShortcut "$SMPROGRAMS\Scan Tailor\Scan Tailor.lnk" "$INSTDIR\scantailor.exe"
  CreateShortcut "$SMPROGRAMS\Scan Tailor\Uninstall.lnk" "$INSTDIR\Uninstaller.exe"  

  # Register the ".ScanTailor" file extension.
  ${registerExtension} "$INSTDIR\scantailor.exe" ".ScanTailor" \
  "Scan Tailor Project" "$INSTDIR\scantailor.exe,0"
SectionEnd


Function un.onInit
  FindWindow $0 "${WNDCLASS}" ""
  StrCmp $0 0 continueInstall
    MessageBox MB_ICONSTOP|MB_OK "The application you are trying to remove is running. Close it and try again."
    Abort
  continueInstall:
FunctionEnd


Section "Uninstall"
  # Unregister the ".ScanTailor" file extension.
  ${unregisterExtension} ".ScanTailor" "Scan Tailor Project"
  
  # Remove program files.
  Delete "$INSTDIR\scantailor.exe"
  Delete "$INSTDIR\mingwm10.dll"
  Delete "$INSTDIR\CrashReporter.exe"
  Delete "$INSTDIR\Uninstaller.exe"
  !if "${CRT_REDIST_DIR}" != ""
	RMDir /r "$INSTDIR\${CRT_REDIST_DIR}"
  !endif
  
  # Remove translations.
  Delete "$INSTDIR\translations\*.qm"
  RMDir "$INSTDIR\translations"
  
  # Remove the installation directory.
  RMDir "$INSTDIR"

  # Remove the uninstall record from the registry.
  DeleteRegKey HKLM "${UNINST_REGKEY}"

  # Remove menu entries
  SetShellVarContext all
  RMDir /r "$SMPROGRAMS\Scan Tailor"
SectionEnd
